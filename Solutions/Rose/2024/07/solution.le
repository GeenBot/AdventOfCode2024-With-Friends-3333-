use std/collections/array;
use std/split;
use std/cast;
use std/math;
use std/io;

fn concat(i64 a, i64 b) -> i64 {
    if b < 10 { return a * 10 + b; }
    if b < 100 { return a * 100 + b; }
    if b < 1000 { return a * 1000 + b; }
}

fn is_valid(i64 target, Array<i64> *numbers, bool part2) -> bool {
    i32 operations = 2 + part2;
    i32 count = numbers.len() - 2;
    i64 combinations = math::pow(operations, count);
    i64 result;

    for i32 i = 0; i < combinations; i += 1 {
        result = numbers[1];
        i32 combination = i;

        for i32 j = 0; j < count; j += 1 {
            i32 operation = combination % operations;
            combination /= operations;

            if operation == 0b00 {
                result += numbers[j + 2];
                continue;
            }

            if operation == 0b01 {
                result *= numbers[j + 2];
                continue;
            }

            if part2 && operation == 0b10 {
                result = concat(result, numbers[j + 2]);
                continue;
            }

            if result > target {
                break;
            }
        }

        if result == target {
            return true;
        }
    }

    return false;
}

fn solution(i32 length, Array<i64> **number_arrays, bool part2) -> i64 {
    i64 total = 0;

    for i32 i = 0; i < length; i += 1 {
        Array<i64> *numbers = number_arrays[i];

        if is_valid(numbers[0], numbers, part2) {
            total += numbers[0];
        }
    }

    return total;
}

fn main(i32 argc, string *argv) {
    string contents = io::read_to_string(argv[1]);
    Array<string> *lines = contents.split("\n");
    Array<i64> *number_arrays[lines.len()];
    defer lines.free();

    for i32 j = 0; j < lines.len(); j += 1 {
        Array<string> *parts = lines[j].split(": ");
        defer parts.free();

        i64 target = i64::parse(parts[0]);
        Array<string> *strings = parts[1].split(" ");
        defer strings.free();

        Array<i64> *numbers = Array::new(target);

        for i32 i = 0; i < strings.len(); i += 1 {
            numbers.push(i64::parse(strings[i]));
        }

        number_arrays[j] = numbers;
    }

    io::dbg(solution(lines.len(), number_arrays, false));
    io::dbg(solution(lines.len(), number_arrays, true));
}
