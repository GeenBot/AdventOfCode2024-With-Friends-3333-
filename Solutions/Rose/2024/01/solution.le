use util;

fn lists(string input) {
    let parts = input.split("\n").map<i64[]>(string::nums);
    let ls = parts.map<i64>(fn(i64[] nums) nums[0]);
    let rs = parts.map<i64>(fn(i64[] nums) nums[1]);

    return $(ls, rs);
}

fn s1(i64[] ls, i64[] rs) {
    let lss = ls.clone();
    let rss = rs.clone();
    lss.sort();
    rss.sort();

    let dist = 0;

    for let i = 0; i < lss.len(); i += 1 {
        dist += math::abs(lss[i] - rss[i]);
    }

    return dist;
}

fn s2(i64[] ls, i64[] rs) {
    i64[] counts = [];

    for r in rs {
        while r >= counts.len() {
            counts.push(0);
        }

        counts[r] += 1;
    }

    let score = 0;

    for l in ls {
        if l < counts.len() {
            score += l * counts[l];
        }
    }

    return score;
}

fn main(string[] args) {
    let res = lists(io::read_to_string(args[1]));
    let ls = res.x;
    let rs = res.y;

    io::dbg(s1(ls, rs));
    io::dbg(s2(ls, rs));
}
